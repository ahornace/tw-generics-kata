apply plugin: 'build-dashboard'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'java'

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '15.0'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    testCompileOnly group: 'org.jetbrains', name: 'annotations', version: '15.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

tasks.withType(FindBugs) {
    excludeFilter = rootProject.file('config/findbugs/excludeBugsFilter.xml')
    reports {
        html.enabled true
        xml.enabled false
    }
}

clean {
    delete 'out'
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

checkstyleMain {
    configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

checkstyleTest {
    configFile = rootProject.file('config/checkstyle/checkstyle-test.xml')
}

findbugs {
    sourceSets = [sourceSets.main]
}

jacoco {
    toolVersion = '0.7.10-SNAPSHOT'
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 1.00
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}
