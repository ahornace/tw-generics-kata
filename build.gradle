buildscript {
    ext {
        checkstyleVersion = '8.12'
        jacocoVersion = '0.8.2'
        lombokVersion = '1.18.2'
        spotbugsVersion = '3.1.7'
    }
}

plugins {
    id 'build-dashboard'
    id 'checkstyle'
    id 'com.github.spotbugs' version '1.6.3'
    id 'jacoco'
    id 'java'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
}

defaultTasks 'clean', 'build'

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.compilerArgs += ['-Werror', '-Xlint:all', '-Xlint:-processing']
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat = 'full'
    }
}

tasks.withType(FindBugs) {
    reports {
        html.enabled true
        xml.enabled false
    }
}

clean {
    delete 'out'
}

jacoco {
    toolVersion = jacocoVersion
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 1.00
            }
        }
    }
}

spotbugs {
    toolVersion = spotbugsVersion
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    showProgress = false

    reports {
        xml.enabled false
        html.enabled true
    }
}

checkstyle {
    toolVersion = checkstyleVersion
}

tasks.withType(Checkstyle) {
    maxWarnings = 0

    reports {
        html.enabled = true
        xml.enabled = false
    }
}


check {
    dependsOn jacocoTestCoverageVerification
}
